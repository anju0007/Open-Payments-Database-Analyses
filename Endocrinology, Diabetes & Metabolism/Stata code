clear
*******************************************************************************
**Import dataset after manual data validation
cd "Endocrinology, Diabetes & Metabolism/profile/"
import delimited using ///
"npi_dataset_payment2014-2022.csv", clear

describe
drop providerotherorganizationnametyp providerotherorganizationname ///
authorizedofficialtitleorpositio  index v55

tab year

**Make variable concerning year of NPPES activation
gen year2 = year

**Make variable for sum of general payments and research payments
gen totalg= general2022 + general2014 + general2015 + general2016 +  ///
general2017 + general2018 + general2019 + general2020 + general2021

gen totalr = research2022 + research2014 + research2015 + research2016 + ///
research2017 + research2018 + research2019 + research2020 + research2021 

gen totalrd = researchd2022 + researchd2014 + researchd2015 + researchd2016 + ///
researchd2017 + researchd2018 + researchd2019 + researchd2020 + researchd2021 

//Make number of general payment contracts
gen totalgc = generalc2022 + generalc2014 + generalc2015 + generalc2016 +  ///
generalc2017 + generalc2018 + generalc2019 + generalc2020 + generalc2021 

//Make general payments excluding ownership and royalty
gen totalgpeor = totalg - (c_ownership2014+c_ownership2019+c_ownership2020 ///
+c_ownership2021+c_ownership2022) - (royalty2014 + royalty2015+royalty2016+ ///
royalty2017 + royalty2018 + royalty2019 + royalty2020 +royalty2021)

**Generate dummy variable for the payment reciept
gen anyg = 1 if totalg>0
replace anyg= 0 if totalg==0

gen anyr = 1 if totalr >0
replace anyr = 0 if totalr==0

gen anyrd = 1 if totalrd >0
replace anyrd = 0 if totalrd == 0

gen anygor = 1 if totalgpeor > 0
replace anygor = 0 if totalgpeor == 0

//Generate variable of total combined payments over nine years
gen total = totalg +totalr + totalrd

//Generate variable of total research payments including direct and associated research payments 
gen totalresearch = totalr + totalrd
tabstat totalresearch totalg, stat(sum) format(%15.0fc) 

gen any_research = 1 if totalresearch>0
replace any_research = 0 if totalresearch == 0
tab any_research
//Generate dummy variable of receipt of any payments over nine years
gen any = 1 if total >0
replace any = 0 if total == 0

**Descriptive analysis
//total payments
tabstat total, by(any) stat(q mean sd) format(%15.0fc)
tabstat total, by(any) ///
stat(mean sd q max min sum) format(%16.0fc) 

tab any //Acceptance of industry payments
inequal7 total  //Gini Index

//Sum of each payments over nine years
tabstat total totalg totalr totalrd totalgpeor, stat(sum) format(%15.0fc)

//General payments
tab anygor
tabstat totalgpeor, by(anygor) ///
stat(mean sd q max min sum) format(%15.2fc)
inequal7 totalgpeor 

gsort -totalgpeor
gen id=_n

//generate variable for top 1%, 5%, and 10% endocrinologists
gen top1 = 1 if id <=80 
replace top1 = 0 if id > 80

gen top5 = 1 if id <= 400
replace top5 = 0 if id >400

gen top10 = 1 if id <= 800
replace top10 = 0 if id > 800

//Payments among top 1% endocrinologists (n=80)
tabstat totalgpeor if id<80, stat(q mean sd) format(%12.0fc)

//Associated research payments
tab anyr
tabstat totalr, by(anyr) stat(mean sd q max min sum) format(%15.2fc)
inequal7 totalr //Gini index

//Direct research payments
tab anyrd
tabstat totalrd, by(anyrd) ///
stat(mean sd q max min sum) format(%15.2fc)
inequal7 totalrd //Gini index


//Endocrinologists receiving research and non-research payments
tab any_research anygor, row chi


//Lorenz Curve
lorenz estimate totalgpeor totalrd totalr, ///
nquantiles(100) percent gini 


*****************************************************
//yearly trend analyses
reshape long general research researchd generall generalc generallc ///
noncme_speaking cme_speaking consulting education ///
honoraria  meal gift travel other royalty c_ownership ///
acquisitions device_loan entertain charity debtforgiveness grant, ///
 i(npi) j(yr) 


drop anyr anyg anyrd anygor


//Analysis of payments by year and trend analysis
**# Bookmark #2
sort general

foreach x of varlist ///
general research researchd generall generalc generallc ///
noncme_speaking cme_speaking consulting education ///
honoraria  meal gift travel other royalty c_ownership ///
acquisitions device_loan entertain charity debtforgiveness grant {
replace `x' = 0 if `x' ==.
}

gen yearnpi = year
drop year
rename yr year
  
gen anyg= 0 if general ==0
replace anyg = 1 if general >0
gen anyr = 0 if research ==0
replace anyr = 1 if research >0
gen anyrd = 0 if researchd ==0
replace anyrd = 1 if researchd >0

gen generalor = general - c_ownership - royalty
gen anygor = 1 if generalor > 0
replace anygor = 0 if generalor == 0

//Total amounts of general, direct research, and associated research payments
tabstat general generall generalor ///
research researchd, by(year) ///
stat(sum) format(%15.0fc)

//general payments by year
tabstat generalor if anygor == 1, by(year) ///
stat(q mean sd max min) format(%9.0fc)
tabstat generalor, stat(sum) format(%12.0fc)

tabstat generalor if anygor == 1, by(year) ///
stat(min) format(%9.1fc)

tab anygor year, col

lorenz estimate generalor, percent over(year) gini
lorenz estimate generalor, nquantiles(100) percent over(year) //Gini index and Lorenz curves by year


//Direct research payments
tabstat researchd if anyrd == 1, by(year) ///
stat(q mean sd max min) format(%10.0fc)
tab year anyrd, row chi
lorenz estimate researchd, percent over(year) gini

//Associated research payments
tabstat research if anyr == 1, by(year) ///
stat(q mean sd max min) format(%9.0fc)
tab year anyr, row chi
lorenz estimate research, percent over(year) gini
tabstat research if research >0, stat(q mean sd) ///
format(%10.0fc) 

tabstat research researchd, by(year) ///
stat(sum) format(%16.0fc)


//yearly trend analysis
xtset npi year,yearly
//Generate dummy variable before and during COVID-19 period (level change variable)
gen covid= 1 if year >2019
replace covid = 0 if year <= 2019
//Generate variable concerning year since the COVID-19 pandemic (post slope variable)
gen since = year -2019 if year >2019
replace since = 0 if year <= 2019

count if research == 0

********************************************************************************
foreach v in general research researchd generall generalc generallc ///
noncme_speaking cme_speaking consulting education ///
honoraria  meal gift travel other royalty c_ownership ///
acquisitions device_loan entertain charity debtforgiveness grant {
gen any`v' = 1 if `v' >0
replace any`v' = 0 if `v' == 0
}

drop totalresearch
foreach v in general research researchd generall generalc generallc ///
noncme_speaking cme_speaking consulting education ///
honoraria  meal gift travel other royalty c_ownership ///
acquisitions device_loan entertain charity debtforgiveness grant {
	egen total`v' = total(`v'), by(npi)
}


//round down associated research payments
gen research_r = round(research/10000)

//ITS analyses of per-physician payments using negative binomial GEE regression
**All general payments
***model 1 including all registered physicians 
xtnbreg general year i.covid since, nolog ///
pa vce(robust) irr ///
pformat(%5.4f) cformat(%5.3f)

***model 2 excluding physicians without any general payments
xtnbreg generall year i.covid since, nolog ///
pa vce(robust) irr ///
pformat(%5.4f) cformat(%5.3f) // final model

//Reciept of general payments
xtgee anyg year i.covid since, ///
family(poisson) link(log) ///
nolog vce(robust) eform pformat(%5.4f) cformat(%5.3f)

//GEE by limited general payment category
gen anygl = 1 if generall >0
replace anygl = 0 if generall ==0

xtgee anygl year i.covid since, ///
family(poisson) link(log) ///
nolog vce(robust) eform  ///
pformat(%5.4f) cformat(%5.4f) //final model

//associated research payments
***ITS GEE model
xtnbreg research year i.covid since, ///
pa vce(robust) irr ///
pformat(%5.4f) cformat(%5.3f) // final model

***receipt of associated research payments
xtgee anyr year i.covid since, ///
family(poisson) link(log)  ///
vce(robust) eform pformat(%5.4f) ///
cformat(%5.3f) //final model

//direct research payments 
***ITS GEE model
xtnbreg researchd year i.covid since , ///
pa vce(robust) irr pformat(%5.4f) cformat(%5.3f) // final model

***GEE model (year was handled as a continuous variable)
xtnbreg researchd year, ///
pa vce(robust) irr pformat(%5.4f) cformat(%5.3f)

***receipt of direct research payments
xtgee anyrd year i.covid since, ///
family(poisson) link(log) ///
vce(robust) eform pformat(%5.4f) cformat(%5.3f) //final model


//Analysis by payment amounts
gen groupo = 0 if totalgenerall ==0
replace groupo = 1 if totalgenerall > 0 & totalgenerall <= 500
replace groupo = 2 if totalgenerall > 500 & totalgenerall <= 1000
replace groupo = 3 if totalgenerall > 1000 & totalgenerall <= 10000
replace groupo = 4 if totalgenerall > 10000 & totalgenerall <= 100000
replace groupo = 5 if totalgenerall > 100000 & totalgenerall <= 500000
replace groupo = 6 if totalgenerall > 500000
rename groupo group2
tab group2

//Trend in payments by payment group
foreach n of numlist 1/6 {
xtnbreg generall year i.covid since ///
if group2 == `n', pa vce(robust) irr ///
pformat(%5.4f) cformat(%5.3f)
}

tabstat generalor noncme_speaking cme_speaking consulting education ///
honoraria  meal gift travel acquisitions device_loan entertain ///
charity debtforgiveness grant, ///
by(top10) stat(sum) format(%12.0fc)
